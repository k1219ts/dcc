//---------------------------------//
// AEZelosFurPtcBinderTemplate.mel //
//-------------------------------------------------------//
// author: Wanho Choi @ Dexter Studios                   //
// last update: 2014.09.02                               //
//-------------------------------------------------------//

///////////////////
// Append Button //
global proc ZelosFurPtcBinderAppendNew( string $attr )
{
	setUITemplate -pst attributeEditorTemplate;
		rowLayout -nc 1;
			button -w 200 -l "Append" -c ( "setAttr " + $attr + " 1" ) ZelosFurPtcBinderAppend;
			setParent ..;
	setUITemplate -ppt;
}

global proc ZelosFurPtcBinderAppendReplace( string $attr )
{
	button -e -c ( "setAttr " + $attr + " 1" ) ZelosFurPtcBinderAppend;
}

//////////
// Path //
global proc ZelosFurPtcBinderPathNew( string $attr )
{
	setUITemplate -pst attributeEditorTemplate;
		rowLayout -nc 4;
			text "Cache Path";
			textField -ed 1 tfZelosFurPtcBinderPath;
			symbolButton -image "navButtonBrowse.xpm"
				-c ( "ZLoadDirectory(\""+$attr+"\",\"tfZelosFurPtcBinderPath\")" ) tfZelosFurPtcBinderPathBtn;
			setParent ..;
	setUITemplate -ppt;
}

global proc ZelosFurPtcBinderPathReplace( string $attr )
{
	textField -e -tx `getAttr $attr` tfZelosFurPtcBinderPath;
	symbolButton -e -c ( "ZLoadDirectory(\""+$attr+"\",\"tfZelosFurPtcBinderPath\")" ) tfZelosFurPtcBinderPathBtn;
}

//////////
// main //
global proc AEZelosFurPtcBinderTemplate( string $nodeName )
{
	editorTemplate -beginScrollLayout;

		editorTemplate -beginLayout "ZelosFurPtcBinder Attributes" -collapse 0;
			editorTemplate -l "Ptc. Group Count" -ac "numPtcGroups";
			editorTemplate -as;
			editorTemplate -l "Mode"             -ac "mode";
			editorTemplate -as;
			editorTemplate -l "Ptc. Group ID"    -ac "ptcGroupId";
			editorTemplate -ccu ZelosFurPtcBinderAppendNew ZelosFurPtcBinderAppendReplace "append";
			editorTemplate -as;
			editorTemplate -ccu ZelosFurPtcBinderPathNew ZelosFurPtcBinderPathReplace "path";
		editorTemplate -endLayout;

		editorTemplate -addExtraControls;

	editorTemplate -endScrollLayout;

	editorTemplate -suppress "inTime";
	editorTemplate -suppress "inViewers";
	editorTemplate -suppress "output";
}

