//------------------------------------//
// AEZelosFurModifier_CutTemplate.mel //
//-------------------------------------------------------//
// author: Wanho Choi @ Dexter Studios                   //
// last update: 2015.03.18                               //
//-------------------------------------------------------//

//////////////
// Se.Expr0 // 
global proc ZelosFurModifier_CutSeExpr0New( string $attr )
{
	setUITemplate -pst attributeEditorTemplate;
		rowLayout -nc 3;
			text "Cut Expr.";
			textField -w 200 -ed 0 tfZelosFurModifier_CutSeExpr0;
			button -w 40 -l "Edit" -c ("ZelosFurModifier_CutSeExpr0Win(\""+$attr+"\")") btnZelosFurModifier_CutSeExpr0;
			setParent ..;
	setUITemplate -ppt;

	ZelosFurModifier_CutSeExpr0Replace( $attr );
}

global proc ZelosFurModifier_CutSeExpr0Replace( string $attr )
{
	textField -e -tx `getAttr $attr` tfZelosFurModifier_CutSeExpr0;
	button -e -c ("ZelosFurModifier_CutSeExpr0Win(\""+$attr+"\")") btnZelosFurModifier_CutSeExpr0;
}

global proc ZelosFurModifier_CutSeExpr0Win( string $attr )
{
    if( `window -q -exists ZelosFurModifierWin` )
	{
    	deleteUI ZelosFurModifierWin;
	}

	window -wh 10 10 -resizeToFitChildren true -titleBar true -sizeable false -title "Expression Editor" ZelosFurModifierWin;
		columnLayout;
			scrollField -wordWrap true -w 300 -h 100 -text `getAttr $attr` sfZelosFurModifier_CutSeExpr0;
		rowLayout -nc 1;
			rowColumnLayout -nc 1;
				button -w 300 -l "Update" -c ("ZelosFurModifier_CutSeExpr0Update(\""+$attr+"\")");
				setParent ..;
			setParent ..;
			rowColumnLayout -nc 2;
				button -w 150 -l "Close"  -c ("deleteUI ZelosFurModifierWin");
				button -w 150 -l "Reset"  -c ("ZelosFurModifier_CutSeExpr0Reset(\""+$attr+"\")");
				setParent ..;
			setParent ..;
	showWindow ZelosFurModifierWin;
}

global proc ZelosFurModifier_CutSeExpr0Update( string $attr )
{
	string $txt = `scrollField -q -tx sfZelosFurModifier_CutSeExpr0`;
	setAttr -type "string" $attr $txt;
	textField -e -tx $txt tfZelosFurModifier_CutSeExpr0;
}

global proc ZelosFurModifier_CutSeExpr0Reset( string $attr )
{
	string $txt = "cut + rand*znoise(freq,x,y,z)";
	textField -e -tx $txt tfZelosFurModifier_CutSeExpr0;
	scrollField -e -tx $txt sfZelosFurModifier_CutSeExpr0;
}

//////////////
// Se.Expr1 // 
global proc ZelosFurModifier_CutSeExpr1New( string $attr )
{
	setUITemplate -pst attributeEditorTemplate;
		rowLayout -nc 3;
			text "Ratio Expr.";
			textField -w 200 -ed 0 tfZelosFurModifier_CutSeExpr1;
			button -w 40 -l "Edit" -c ("ZelosFurModifier_CutSeExpr1Win(\""+$attr+"\")") btnZelosFurModifier_CutSeExpr1;
			setParent ..;
	setUITemplate -ppt;

	ZelosFurModifier_CutSeExpr1Replace( $attr );
}

global proc ZelosFurModifier_CutSeExpr1Replace( string $attr )
{
	textField -e -tx `getAttr $attr` tfZelosFurModifier_CutSeExpr1;
	button -e -c ("ZelosFurModifier_CutSeExpr1Win(\""+$attr+"\")") btnZelosFurModifier_CutSeExpr1;
}

global proc ZelosFurModifier_CutSeExpr1Win( string $attr )
{
    if( `window -q -exists ZelosFurModifierWin` )
	{
    	deleteUI ZelosFurModifierWin;
	}

	window -wh 10 10 -resizeToFitChildren true -titleBar true -sizeable false -title "Expression Editor" ZelosFurModifierWin;
		columnLayout;
			scrollField -wordWrap true -w 300 -h 100 -text `getAttr $attr` sfZelosFurModifier_CutSeExpr1;
		rowLayout -nc 1;
			rowColumnLayout -nc 1;
				button -w 300 -l "Update" -c ("ZelosFurModifier_CutSeExpr1Update(\""+$attr+"\")");
				setParent ..;
			setParent ..;
			rowColumnLayout -nc 2;
				button -w 150 -l "Close"  -c ("deleteUI ZelosFurModifierWin");
				button -w 150 -l "Reset"  -c ("ZelosFurModifier_CutSeExpr1Reset(\""+$attr+"\")");
				setParent ..;
			setParent ..;
	showWindow ZelosFurModifierWin;
}

global proc ZelosFurModifier_CutSeExpr1Update( string $attr )
{
	string $txt = `scrollField -q -tx sfZelosFurModifier_CutSeExpr1`;
	setAttr -type "string" $attr $txt;
	textField -e -tx $txt tfZelosFurModifier_CutSeExpr1;
}

global proc ZelosFurModifier_CutSeExpr1Reset( string $attr )
{
	string $txt = "0.0";
	textField -e -tx $txt tfZelosFurModifier_CutSeExpr1;
	scrollField -e -tx $txt sfZelosFurModifier_CutSeExpr1;
}

///////////////////
// Update Button //
global proc ZelosFurModifier_CutUpdateNew( string $attr )
{
	setUITemplate -pst attributeEditorTemplate;
		rowLayout -nc 1;
			rowColumnLayout -nc 1;
				button -w 200 -l "Update" -c ( "setAttr " + $attr + " 1" ) ZelosFurModifier_CutUpdate;
				setParent ..;
			setParent ..;
	setUITemplate -ppt;
}

global proc ZelosFurModifier_CutUpdateReplace( string $attr )
{
	button -e -c ( "setAttr " + $attr + " 1" ) ZelosFurModifier_CutUpdate;
}

//////////////////
// texture list //
global proc ZelosFurModifier_CutTextureListNew( string $attr )
{
	setUITemplate -pst attributeEditorTemplate;
		columnLayout;
			rowLayout;
				optionMenu -w 200 omZelosFurModifier_CutTextureList;
				setParent ..;
			rowLayout -nc 1;
				rowColumnLayout -nc 2;
					button -w 200 -l "Select" -c "select -r `optionMenu -q -v omZelosFurModifier_CutTextureList`";
					setParent ..;
				setParent ..;
			setParent ..;
	setUITemplate -ppt;

	ZelosFurModifier_CutTextureListReplace( $attr );
}

global proc ZelosFurModifier_CutTextureListReplace( string $attr )
{
	// remove the old menu items of the option menu.
	string $menuItems[] = `optionMenu -q -ill omZelosFurModifier_CutTextureList`;
	for( $menuItem in $menuItems ) { deleteUI $menuItem; }

	// get the name of this ZelosFurModifier_Cut node.
	string $node[];
	tokenize( $attr, ".", $node );
	string $ZelosFurModifier_CutNode = $node[0];

	// Get the connected nodes.
	string $connectedPlg[] = `listAttr -multi ( $ZelosFurModifier_CutNode + ".filePathNames" )`;
	int $numConnections = size($connectedPlg);

	// Add the connected node names to the option menu.
	int $i=0;
	for( ; $i<$numConnections; $i++ )
	{
		if( !`connectionInfo -id ( $ZelosFurModifier_CutNode + "." + $connectedPlg[$i] )` )
		{
			continue;
		}

		string $nodeAttr = `connectionInfo -sfd ( $ZelosFurModifier_CutNode + "." + $connectedPlg[$i] )`;
		tokenize( $nodeAttr, ".", $node );
		menuItem -label $node[0] -p omZelosFurModifier_CutTextureList;
	}
}

////////////////////
// generator list //
global proc ZelosFurModifier_CutGeneratorListNew( string $attr )
{
	setUITemplate -pst attributeEditorTemplate;
		columnLayout;
			rowLayout;
				optionMenu -w 200 omZelosFurModifier_CutGeneratorList;
				setParent ..;
			rowLayout -nc 1;
				rowColumnLayout -nc 2;
					button -w 200 -l "Select" -c "select -r `optionMenu -q -v omZelosFurModifier_CutGeneratorList`";
					setParent ..;
				setParent ..;
			setParent ..;
	setUITemplate -ppt;

	ZelosFurModifier_CutGeneratorListReplace( $attr );
}

global proc ZelosFurModifier_CutGeneratorListReplace( string $attr )
{
	// remove the old menu items of the option menu.
	string $menuItems[] = `optionMenu -q -ill omZelosFurModifier_CutGeneratorList`;
	for( $menuItem in $menuItems ) { deleteUI $menuItem; }

	// get the name of this ZelosFurModifier_Cut node.
	string $node[];
	tokenize( $attr, ".", $node );
	string $ZelosFurModifier_CutNode = $node[0];

	// Get the connected nodes.
	string $connectedPlg[] = `listAttr -multi ( $ZelosFurModifier_CutNode + ".outModifier" )`;
	int $numConnections = size($connectedPlg);

	// Add the connected node names to the option menu.
	int $i=0;
	for( ; $i<$numConnections; $i++ )
	{
		if( !`connectionInfo -is ( $ZelosFurModifier_CutNode + "." + $connectedPlg[$i] )` )
		{
			continue;
		}

		string $nodeAttr[] = `connectionInfo -dfs ( $ZelosFurModifier_CutNode + "." + $connectedPlg[$i] )`;
		tokenize( $nodeAttr[0], ".", $node );
		menuItem -label $node[0] -p omZelosFurModifier_CutGeneratorList;
	}
}

//////////
// main //
global proc AEZelosFurModifier_CutTemplate( string $nodeName )
{
	editorTemplate -beginScrollLayout;

		editorTemplate -beginLayout "Controls" -collapse 0;
			editorTemplate -l "Turn On"                                   -ac "onOff";
			editorTemplate -ccu ZTextBarNew ZTextBarReplace "Cut Value";
			editorTemplate -l "Cut [cut]"                                 -ac "cut";
			editorTemplate -as;
			editorTemplate -l "Randomness [rand]"                         -ac "rand0";
			editorTemplate -l "Frequency [freq]"                          -ac "freq0";
			editorTemplate -as;
			editorTemplate -ccu ZelosFurModifier_CutSeExpr0New ZelosFurModifier_CutSeExpr0Replace "seExpr0Str";
			editorTemplate -ccu ZTextBarNew ZTextBarReplace "Exception Ratio";
			editorTemplate -ccu ZelosFurModifier_CutSeExpr1New ZelosFurModifier_CutSeExpr1Replace "seExpr1Str";
			editorTemplate -as;
			editorTemplate -ccu ZelosFurModifier_CutUpdateNew ZelosFurModifier_CutUpdateReplace "update";
		editorTemplate -endLayout;

		editorTemplate -beginLayout "Connected Texture List" -collapse 0;
			editorTemplate -ccu ZelosFurModifier_CutTextureListNew ZelosFurModifier_CutTextureListReplace "";
		editorTemplate -endLayout;

		editorTemplate -beginLayout "Connected Generator List" -collapse 0;
			editorTemplate -ccu ZelosFurModifier_CutGeneratorListNew ZelosFurModifier_CutGeneratorListReplace "";
		editorTemplate -endLayout;

		editorTemplate -addExtraControls;

	editorTemplate -endScrollLayout;

	editorTemplate -suppress "filePathNames";
	editorTemplate -suppress "outModifier";
	editorTemplate -suppress "seed0";
}

