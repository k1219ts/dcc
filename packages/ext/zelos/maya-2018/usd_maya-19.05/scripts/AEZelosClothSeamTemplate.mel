//------------------------------//
// AEZelosClothSeamTemplate.mel //
//-------------------------------------------------------//
// author: Wanho Choi @ Dexter Studios                   //
//         Inyong Jeon @ SNU University                  //
// last update: 2015.05.07                               //
//-------------------------------------------------------//

//////////////////////
// connnection info //
global proc ZelosClothSeamConnectionInfoNew( string $attr )
{
	setUITemplate -pst attributeEditorTemplate;
		text -l "Input ZelosClothCurve Nodes" -bgc 0.2 0.2 0.2;
	setUITemplate -ppt;

	setUITemplate -pst attributeEditorTemplate;
		columnLayout;
			rowLayout;
				optionMenu -w 200 -cc "select -r `optionMenu -q -v omZelosClothSeamList0`" omZelosClothSeamList0;
				setParent..;
			setParent..;
	setUITemplate -ppt;

	ZelosClothSeamConnectionInfoReplace( $attr );
}

global proc ZelosClothSeamConnectionInfoReplace( string $attr )
{
	string $node[];
	tokenize( $attr, ".", $node );
	string $ZelosClothSeam = $node[0];

	// Input Nodes (ZelosClothCurve nodes)
	{
		string $items[] = `optionMenu -q -ill omZelosClothSeamList0`;
		for( $item in $items ) { deleteUI $item; }

		string $plgs[] = `listAttr -multi ( $ZelosClothSeam + ".curves" )`;

		for( $plg in $plgs )
		{
			string $nodeAttr = `connectionInfo -sfd ( $ZelosClothSeam + "." + $plg )`;
			tokenize( $nodeAttr, ".", $node );

			if( `nodeType $node[0]` == "ZelosClothCurve" )
			{
				menuItem -label $node[0] -p omZelosClothSeamList0;
			}
		}
	}
}

//////////
// main //
global proc AEZelosClothSeamTemplate( string $nodeName )
{
	editorTemplate -beginScrollLayout;

		editorTemplate -beginLayout "Display" -collapse 0;
			editorTemplate -l "Display"        -ac "display";
			editorTemplate -l "Line Color"     -ac "lineColor";
		editorTemplate -endLayout;

		editorTemplate -beginLayout "Control" -collapse 0;
			editorTemplate -l "Active"         -ac "active";
			editorTemplate -l "Reverse"        -ac "reverse";
			editorTemplate -l "Type"           -ac "type";
		editorTemplate -endLayout;

		editorTemplate -beginLayout "Connected Node List" -collapse 0;
			editorTemplate -ccu ZelosClothSeamConnectionInfoNew ZelosClothSeamConnectionInfoReplace "";
		editorTemplate -endLayout;

		editorTemplate -addExtraControls;

	editorTemplate -endScrollLayout;

	editorTemplate -suppress "curves";
	editorTemplate -suppress "patches";
	editorTemplate -suppress "numSegs";
	editorTemplate -suppress "output";
	editorTemplate -suppress "mode";
}

